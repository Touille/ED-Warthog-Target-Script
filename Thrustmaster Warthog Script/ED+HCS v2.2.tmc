//	Copyright (C) 2017 Thomas Duployez (CMDR Touille)
//	V2.2.03.2b	2017/01/14
//	Distributed freely under the MIT License.
//	
//	Permission is hereby granted, free of charge, to any person obtaining a copy
//	of this software and associated documentation files (the "Software"), to deal
//	in the Software without restriction, including without limitation the rights
//	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//	copies of the Software, and to permit persons to whom the Software is
//	furnished to do so, subject to the following conditions:
//	
//	The above copyright notice and this permission notice shall be included in all
//	copies or substantial portions of the Software.
//	
//	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//	SOFTWARE.
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	INCLUDED FILES																																				//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

include "target.tmh"
include "ED+HCS v2.2_KeyBindings.ttm"
include "ED+HCS v2.2_Settings.tmh"
//include "ED+HCS v2.2_Functions.tmh"
include "ED+HCS v2.2_Functions_TOGGLE .tmh"
include "ED+HCS v2.2_Functions_CORE.tmh"
include "ED+HCS v2.2_Macros.tmh"

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
int main()
{
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	Configure(&HCougar, MODE_EXCLUDED);
	Configure(&T16000, MODE_EXCLUDED);
	Configure(&T16000L, MODE_EXCLUDED);
	Configure(&TWCSThrottle, MODE_EXCLUDED);

	//Rudders
	if(Rudders < 2)Configure(&TFRPRudder, MODE_EXCLUDED);
	else Configure(&TFRPRudder, MODE_FILTERED);

	//MFD
	if(USEMFD == 0){
		Configure(&LMFD, MODE_EXCLUDED);
		Configure(&RMFD, MODE_EXCLUDED);
	}

	if(Init(&EventHandle)) return 1; // declare the event handler, return on error

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	BASE SETTINGS																																				//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	SetKBRate(PulseTime, DelayTime);
	SetKBLayout(KBLayout);
	
	//Initiate Rudders
	if(Rudders == 2)initRudders();

	// Initiate Axis Mappings
	if(DebugMode)printf("-------------------------Hardware Initialisation--------------------------\x0a");
	
	if(DebugMode)printf("Initialising Joystick Axis\x0a");
	initJoystickAxis();
	
	if(DebugMode)printf("Initialising Throttle Axis\x0a");
	initThrottleAxis();
	
	if(DebugMode)printf("Initialising Joystick Curve\x0a");
	initSetJoystickCurves();
	
	if(DebugMode)printf("Initialising Throttle Curve\x0a");
	initSetThrottleCurves();

	//Initiate MFD
	if(USEMFD > 0){
		if(DebugMode)printf("Initialising Right MFD\x0a");
		initRightMFD();
	}
	if(USEMFD > 1){
		if(DebugMode)printf("Initialising Left MFD\x0a");
		initLeftMFD();
	}

	// Initiate Led State
	if(DebugMode)printf("Initialising LED States\x0a");
	initLEDStates();

	// Initiate Custom & PIP Functions (ED+HCS_Macros_v2.2.tmh)
	initCustomCommands();
	
	if(DebugMode)initPrintState();

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	;)																																							//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	printf("--------------------------V2.2.03.2b----2017/01/14------------------------\x0a");
	printf("---------Enhanced Elite Dangerous Warthog Profile by CMDR Touille---------\x0a");
	printf("------------------------------Fly Safe CMDR! o7---------------------------\x0a");
	printf("-o/--o/--o/-Aussiedroid-DarKcyde0-WraithMG11235-Synkc-Solarfly-o/--o/--o/-\x0a");
	printf("--------------------------------------------------------------------------\x0a");

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	Shift States // Modifier don't touch this																													//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	SetShiftButton(&Joystick, S3, &Throttle, APPAT, APALT);

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//	
//	MAP JOYSTICK CONTROLS																																		//																						
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

//--JOYSTICK BUTTON---------------------------------------------------------------------------------------------------------------------------------------------//

	MapKey(&Joystick, TG1, EXEC("initFire();"));
	MapKeyR(&Joystick, TG1, EXEC("initFire();"));

	MapKey(&Joystick, TG2, 0);	// Reseved for initCombinedFire()

	MapKey(&Joystick, S1, TEMPO(EXEC("initToggleCombinedFire();"), EXEC("initToggleHardpoints();"), LongPressDelay));

	MapKey(&Joystick, S2, EXEC("initFire();"));
	MapKeyR(&Joystick, S2, EXEC("initFire();"));

	//MapKey(&Joystick, S3, 0);	// Modifier S3 don't touch this
	
	MapKey(&Joystick, S4, EXEC(" initToggleEnhancedFAOFF();"));
	MapKeyR(&Joystick, S4, EXEC(" initToggleEnhancedFAOFF();"));

//--JOYSTICK HAT------------------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Trim Switch
	
	// UP		PIPS[Short: 4xENG 1SYS 1WEP,	Med: 4xENG 2xSYS,	Long: 4xENG 2xWEP	S3: 3xSYS 3xWEP]
	MapKeyIO(&Joystick, H1U, EXEC("initPIPPower(SYS,WEP,MID);"),TEMPO(EXEC("initPIPPower(ENG);"), TEMPO(EXEC("initPIPPower(ENG,SYS);"), EXEC("initPIPPower(ENG,WEP);"), PIPPressDelay), PIPPressDelay));

	// DOWN		PIPS[Short: Balance,	Med: Cycle 3xSYS 3xWEP / 3xENG 3xWEP / 3ENG 3xSYS]
	MapKey(&Joystick, H1D, TEMPO(EXEC("initPIPPower();"), SEQ(EXEC("initPIPPower(SYS,WEP,MID);"),EXEC("initPIPPower(ENG,WEP,MID);"), EXEC("initPIPPower(ENG,SYS,MID);")), PIPPressDelay));	
	
	// LEFT		PIPS[Short: 4xSYS 1ENG 1WEP,	Med: 4xSYS 2xENG,	Long: 4xSYS 2xWEP	S3: 3ENG 3xSYS]
	MapKeyIO(&Joystick, H1L, EXEC("initPIPPower(SYS,ENG,MID);"),TEMPO(EXEC("initPIPPower(SYS);"), TEMPO(EXEC("initPIPPower(SYS,ENG);"), EXEC("initPIPPower(SYS,WEP);"), PIPPressDelay), PIPPressDelay));
	
	// RIGHT	PIPS[Short: 4xWEP 1SYS 1ENG,	Med: 4xWEP 2xENG,	Long: 4xWEP 2xSYS	S3: 3xENG 3xWEP]
	MapKeyIO(&Joystick, H1R, EXEC("initPIPPower(ENG,WEP,MID);"),TEMPO(EXEC("initPIPPower(WEP);"), TEMPO(EXEC("initPIPPower(WEP,ENG);"), EXEC("initPIPPower(WEP,SYS);"), PIPPressDelay), PIPPressDelay));

	//	-------------------------Target Management Switch (TMS)
	MapKey(&Joystick, H2U, TEMPO(PULSE+SelectTargetAhead, PULSE+SelectHighestThreat, LongPressDelay));													// UP
	MapKey(&Joystick, H2D, PULSE+SelectNextSystemInRoute);																								// DOWN
	MapKey(&Joystick, H2L, TEMPO(PULSE+SelectPrevShip, TEMPO(PULSE+SelectPrevHostileShip, PULSE+SelectPrevSubsystem, LongPressDelay), LongPressDelay));	// LEFT
	MapKey(&Joystick, H2R, TEMPO(PULSE+SelectNextShip, TEMPO(PULSE+SelectNextHostileShip, PULSE+SelectNextSubsystem, LongPressDelay), LongPressDelay));	// RIGHT
	
	//	-------------------------Data Management Switch (DMS)
	MapKeyIO(&Joystick, H3U, PULSE+CommsPanel, TEMPO(EXEC("initToggleSilentRuningOFFonFire();"), EXEC("initToggleHeatSinkonSilentRuning();"), ExtraPressDelay));	// UP
	MapKeyIO(&Joystick, H3D, PULSE+RolePanel, TEMPO(0,EXEC("initToggleSilentRuning();"), LongPressDelay));															// DOWN
	MapKeyRIO(&Joystick, H3D, 0,EXEC("initToggleSilentRuning();"));																									// DOWN RELEASE
	MapKeyIO(&Joystick, H3L, PULSE+TargetPanel, PrevFireGroup); 																									// /LEFT
	MapKeyIO(&Joystick, H3R, PULSE+SystemPanel, NextFireGroup); 																									// RIGHT

	//	-------------------------Countermeasures Management Switch (CMS)
	MapKey(&Joystick, H4U, UIUp);																	// UP		UI UP
	MapKey(&Joystick, H4D, UIDown);																	// DOWN		UI DOWN
	MapKeyIO(&Joystick, H4L, UIPrevPanel, UILeft);													// LEFT		UI LEFT,	[S3]+ Previous Panel
	MapKeyIO(&Joystick, H4R, UINextPanel, UIRight);													// RIGHT	UI RIGHT,	[S3]+ Next Panel
	MapKeyIO(&Joystick, H4P, UIBack, UISelect);														// PUSH		UI SELECT,	[S3]+[S] UI BACK

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	MAP THROTTLE ARM CONTROLS																																	//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

//--THROTTLE ARM BUTTON-----------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Slew Control Push
	MapKey(&Throttle, SC, EXEC("initToogleHeadLook();"));	//ONLY IF !HMD & !TRACKIR : Head Look		
	MapKeyR(&Throttle, SC, EXEC("initToogleHeadLook();"));

	//	-------------------------Left Throttle Button (RED)
	MapKeyIO(&Throttle, LTB, EXEC("initSetThrottleCurves();"), EXEC("initToggleReverseThrottle();"));
	MapKeyRIO(&Throttle, LTB, 0, EXEC("initToggleReverseThrottle();"));

	//	-------------------------Mic Switch
	MapKey(&Throttle, MSP, EXEC("initVoicePTT();"));
	MapKeyR(&Throttle, MSP, EXEC("initVoicePTT();"));

//--THROTTLE ARM HAT--------------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Mic Switch
	MapKeyIO(&Throttle, MSU, TEMPO(PULSE+FighterFocus, PULSE+FighterDock, LongPressDelay), TEMPO(PULSE+WingmanTarget, PULSE+WingmanNavlock, LongPressDelay));	// UP
	MapKeyIO(&Throttle, MSD, TEMPO(PULSE+FighterFollow, PULSE+FighterOrders, LongPressDelay), PULSE+SelectWingman2);											// DOWN
	MapKeyIO(&Throttle, MSL, TEMPO(PULSE+FighterHoldPosition, PULSE+FighterDefensive, LongPressDelay), PULSE+SelectWingman1);									// LEFT
	MapKeyIO(&Throttle, MSR, TEMPO(PULSE+FighterHoldFire, PULSE+FighterAggressive, LongPressDelay), PULSE+SelectWingman3);										// RIGHT
	
	//	-------------------------Coolie Hat Switch	
	MapKey(&Throttle, CSU, ThrustUp);																				// UP
	MapKey(&Throttle, CSD, ThrustDown);																				// DOWN
	MapKey(&Throttle, CSL, ThrustLeft);																				// LEFT
	MapKey(&Throttle, CSR, ThrustRight);																			// RIGHT

//--THROTTLE ARM SWITCH-----------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Speedbrake
	MapKey(&Throttle, SPDF, TEMPO(EXEC("initFrameShiftDrive();"), EXEC("initSystemJump();"), ExtraPressDelay));													// Switch FORWARD
//	MapKey(&Throttle, SPDM, 0);																																	// Switch MIDDLE	
	MapKeyIO(&Throttle, SPDB,  EXEC("initToggleSuperCruiseMode();"), TEMPO(EXEC("initThrottleBoost();"), EXEC("initSuperCruise();"), LongPressDelay));			// Switch BACK HOLD

	//	-------------------------Boat Switch
	MapKey(&Throttle, BSF, PULSE+GalaxyMap);																		// Switch FORWARD
	MapKey(&Throttle, BSM, PULSE+UIBack);																			// Switch MIDDLE
	MapKey(&Throttle, BSB, PULSE+SystemMap);																		// Switch BACK

	//	-------------------------China Hat
	MapKey(&Throttle, CHF, TEMPO(PULSE+DeployHeatSink, PULSE+ChargeECM, LongPressDelay));							// Switch FORWARD
//	MapKey(&Throttle, CHM, 0);																						// Switch MIDDLE
	MapKey(&Throttle, CHB, TEMPO(PULSE+FireChaffLauncher, PULSE+UseShieldCell, LongPressDelay));					// Switch BACK

	//	-------------------------Pinky Throttle
	MapKeyIO(&Throttle, PSF, PULSE+ChangeWeaponColor, PULSE+OrbitalLines);											// Switch FORWARD
//	MapKey(&Throttle, PSM, 0);																						// Switch MIDDLE
	MapKeyIO(&Throttle, PSB, PULSE+ChangeEngineColor, PULSE+HideShipGUI);											// Switch BACK


//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	MAP THROTTLE BASE CONTROLS																																	//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

//--THROTTLE BASE BUTTON----------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Landing Gear/ Warning Horn Silence Button
	MapKeyIO(&Throttle, LDGH, PULSE+TrackIRPause, TEMPO(PULSE+EXEC("initHeadCenter();"), TEMPO(PULSE+Screenshot, PULSE+HighResScreenshot, LongPressDelay), LongPressDelay));			// PUSH

	//	-------------------------Autopilot Engage Button Same As Autopilot Disengage Button
	MapKey(&Throttle, APENG, EXEC("initEngageAPkey();"));																																// PUSH

	//	-------------------------Autopilot Disengage Button Same As Autopilot Engage Button
//	MapKey(&Throttle, APDIS, 0);																																						// <Unmapped> PUSH

//--THROTTLE BASE SWITCH----------------------------------------------------------------------------------------------------------------------------------------//	
	
	//	-------------------------FLAPS - Map Throttle to Joystick Curve Presets
	MapKey(&Throttle, FLAPU, EXEC("initSetJoystickCurves();"));														// Flaps UP
	MapKey(&Throttle, FLAPM, EXEC("initSetJoystickCurves();"));														// Flaps MIDDLE
	MapKey(&Throttle, FLAPD, EXEC("initSetJoystickCurves();"));														// Flaps DOWN

	//	-------------------------Autopilot Select Switch // Modifier don't touch this
//	MapKey(&Throttle, APPAT, 0);																					// Modifier UP			(PATH)
//	MapKey(&Throttle, APAH, 0);																						// Modifier MIDDLE		(ALT/HDG)
//	MapKey(&Throttle, APALT, 0);																					// Modifier DOWN		(ALT)

	//	-------------------------Engine Fuel Flow Switches LEFT
//	MapKey(&Throttle, EFLNORM, 0);																					// Switch UP
//	MapKey(&Throttle, EFLOVER, 0);																					// Switch DOWN

	//	-------------------------Engine Fuel Flow Switches RIGHT
	MapKey(&Throttle, EFRNORM, EXEC("initToggleLights();"));														// Switch UP
	MapKey(&Throttle, EFROVER, EXEC("initToggleLights();"));														// Switch DOWN

	//	-------------------------Engine Operate Ign/Norm LEFT
	MapKeyIO(&Throttle, EOLIGN, TEMPO(EXEC("initTogglePTTPTP();"), EXEC("initTogglePTTMode();"), LongPressDelay), 
								TEMPO(EXEC("initToggleCamera();"), EXEC("initFPSCONNECTION();"), LongPressDelay));	// UP		
//	MapKey(&Throttle, EOLNORM, 0);																					// OFF
	MapKey(&Throttle, EOLMOTOR, EXEC("initToggleVoiceAttackMic();"));												// DOWN
	MapKeyR(&Throttle, EOLMOTOR, EXEC("initToggleVoiceAttackMic();"));												// DOWN RELEASE

	//	-------------------------Engine Operate Ign/Norm RIGHT
	MapKeyIO(&Throttle, EORIGN, TEMPO(EXEC("initToggleSecureRetract();"), EXEC("initToggleRotationCorrection();"), LongPressDelay),
								TEMPO(EXEC("initToggleOptimalPIPMode();"), EXEC("initToggleStopFireRetractHardpoints();"), LongPressDelay));	// UP								
//	MapKey(&Throttle, EORNORM, 0);																												// OFF

	MapKey(&Throttle, EORMOTOR, EXEC("initToggleGameMenu();"));														// DOWN
	MapKeyR(&Throttle, EORMOTOR, EXEC("initToggleGameMenu();"));													// DOWN RELEASE

	//	-------------------------APU Start
	MapKey(&Throttle, APUON, EXEC("initJettisonCargo();"));															// Switch UP
//	MapKey(&Throttle, APUOFF, 0);																					// Switch DOWN (OFF)

	//	-------------------------EAC
	MapKey(&Throttle, EACON, EXEC("initToggleLandingGear();"));														// Switch UP 			(ARM)
	MapKeyR(&Throttle, EACON, EXEC("initToggleLandingGear();"));													// Switch UP RELEASE
//	MapKey(&Throttle, EACOFF, 0);																					// Switch DOWN			(OFF)

	//	-------------------------RDR ALTM
	MapKey(&Throttle, RDRNRM, EXEC("initToggleCargoScoop();"));														// Switch UP			(NRM)
	MapKeyR(&Throttle, RDRNRM, EXEC("initToggleCargoScoop();"));													// Switch UP RELEASE
//	MapKey(&Throttle, RDRDIS, 0);																					// Switch DOWN			(DIS)


//--THROTTLE BASE TRHOTTLE--------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Throttle Idle Detents LEFT
	MapKey(&Throttle, IDLELON, EXEC("initToggleIDLEonThrottle();"));												// ON	[Locking (Down): Stationary]
	MapKeyR(&Throttle, IDLELON, EXEC("initToggleIDLEonThrottle();"));												// ON RELEASE
//	MapKey(&Throttle, IDLELOFF, 0);																					// OFF

	//	-------------------------Throttle Idle Detents RIGHT
//	MapKey(&Throttle, IDLERON, 0);																					// ON
//	MapKeyR(&Throttle, IDLERON, 0);																					// ON RELEASE
//	MapKey(&Throttle, IDLEROFF, 0);																					// OFF

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
}
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	HARDWARE MAPPINGS																																			//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

	//	-------------------------Set LED
	int initLEDStates(){
		//Enable LED Backlight on Start (Value 0-255)
		if(HMD){	//Set LED Backlight on Start If HMD
				vThrottleLight = ThrottleLightHMD;
				vThrottleLightBackup = ThrottleLightHMD;	//Backup the LED Backlight defaukt value
				if(DebugMode)printf("HMD -> Backlight IDLE Throttle LED Set to %d\x0a", ThrottleLightHMD);
				ActKey(PULSE+KEYON+LED(&Throttle, LED_INTENSITY, ThrottleLightHMD));			
		}else{	//Set LED Backlight on Start
				vThrottleLight = ThrottleLight;
				vThrottleLightBackup = ThrottleLight;	//Backup the LED Backlight defaukt value
				if(DebugMode)printf("No HMD Or TrackIR -> Backlight IDLE Throttle LED Set to %d\x0a", vThrottleLight);
				ActKey(PULSE+KEYON+LED(&Throttle, LED_INTENSITY, ThrottleLight));				
		}
	
		//Initiate LED status all OFF
		ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED1));	
		ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED2));
		ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED3));	
		ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED4));
		ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED5));		
		
		//Initiate Led State
		if(ToggleSuperCruiseModeLED!=0 & SupecruiseOnThrottle){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleSuperCruiseModeLED));}
		if(ToggleRCorrectionLED!=0 & sRCorrection){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleRCorrectionLED));}
		if(ToggleLandingGearLED!=0 & sGears){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleLandingGearLED));}
		if(ToggleCargoScoopLED!=0 & sCargo){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleCargoScoopLED));}
		if(ToggleLightsLED!=0 & sLights){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleLightsLED));}
		if(ToggleHardpointsLED!=0 & sHardPoints){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleHardpointsLED));}
		if(ToggleCombinedFireLED!=0 & sCombinedFire){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleCombinedFireLED));}
		if(TogglePTTLED!=0 & sPTTState){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+TogglePTTLED));}
		if(ToggleVoicePTTLED!=0 & vVoicePTT){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleVoicePTTLED));}
		if(TogglePTTModeLED!=0 & sPTTMode){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+TogglePTTModeLED));}
		if(ToggleSilentRuningonFireLED!=0 & SilentRuningOFFonFire){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleSilentRuningonFireLED));}
		if(ToggleHeatSinkonSilentRuningLED!=0 & HeatSinkonSilentRuning){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleHeatSinkonSilentRuningLED));}
		if(ToggleIDLEonThrottleLED!=0 & sIDLE){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleIDLEonThrottleLED));}
		if(ToggleOptimalPIPModeLED!=0 & OptimalPIPMode){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleOptimalPIPModeLED));}
		if(ToggleSecureRetractLED!=0 & SecureRetract){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleSecureRetractLED));}
		if(ToggleStopFireRetractHardpointsLED!=0 & StopFireRetractHardpoints){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleStopFireRetractHardpointsLED));}		
		//if(ToggleHardPointsonFireGameModeLED!=0 & HardPointsonFireGameMode){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleHardPointsonFireGameModeLED));}
		//if(ToggleFALED!=0 & sFA){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleFALED));}
		//if(ToggleSilentRuningLED!=0 & sSilentRuning){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleSilentRuningLED));}
		//if(ToggleCameraLED!=0 & sCameraMode){ActKey(KEYON+PULSE+LED(&Throttle, LED_ONOFF, LED_CURRENT+ToggleCameraLED));}
	}
	
	//	-------------------------Set Joystick
	//Set Default Joystick Axis
	int initJoystickAxis(){	
		MapAxis(&Joystick, JOYX, DX_X_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);							// Set Joystick Control X Axis
		MapAxis(&Joystick, JOYY, DX_Y_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);							// Set Joystick Control Y Axis
		MapAxis(&Throttle, SCX, DX_XROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control X Axis
		MapAxis(&Throttle, SCY, DX_YROT_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);						// Set Slew Control Y Axis
	}

	//Joystick Curve Profile Three different presets are selectable On The Fly (FLAP position)
	int initSetJoystickCurves(){
		if(Throttle[FLAPU]) vJoystickCurveProfile = 0;
		if(!Throttle[FLAPU] & !Throttle[FLAPD]) vJoystickCurveProfile = 1;
		if(Throttle[FLAPD]) vJoystickCurveProfile = 2;
		ActKey(KEYON+PULSE+EXEC("initJoystickCurves();"));
	}
	
	//Joystick Curve Initialising
	int initJoystickCurves(){
		SetSCurve(&Joystick, JOYX, 0, JS_DEADZONE, 0, JS_CURVE[vJoystickCurveProfile], JS_ZOOM[vJoystickCurveProfile]);
		SetSCurve(&Joystick, JOYY, 0, JS_DEADZONE, 0, JS_CURVE[vJoystickCurveProfile], JS_ZOOM[vJoystickCurveProfile]);
		SetSCurve(&Throttle, SCX, 0, SL_DEADZONE, 0, SL_CURVE[vJoystickCurveProfile], SL_ZOOM[vJoystickCurveProfile]);
		SetSCurve(&Throttle, SCY, 0, SL_DEADZONE, 0, SL_CURVE[vJoystickCurveProfile], SL_ZOOM[vJoystickCurveProfile]);
		if (DebugMode){
			printf("Set	Joystick	Curve Profile	[%d]: Curve=%d, Zoom=%d\x0a", vJoystickCurveProfile, JS_CURVE[vJoystickCurveProfile], JS_ZOOM[vJoystickCurveProfile]);
			printf("Set	Slew		Curve Profile	[%d]: Curve=%d, Zoom=%d\x0a", vJoystickCurveProfile, SL_CURVE[vJoystickCurveProfile], SL_ZOOM[vJoystickCurveProfile]);
		}
	}
	
	//	-------------------------Set Throttle
	//Set Default Throttle Axis
	int initThrottleAxis(){	
		MapAxis(&Throttle, THR_RIGHT, DX_Z_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);					// Set Right Throttle
		MapAxis(&Throttle, THR_LEFT, 0, AXIS_NORMAL, MAP_ABSOLUTE);								// Set Left Throttle / DX_ZROT_AXIS / We don't Use It Yet In ED
		MapAxis(&Throttle, THR_FC, DX_SLIDER_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);					// Set Friction Control Axis Curve (Sensor Range Zoom Scaling) 
		SetCustomCurve(&Throttle, THR_FC, LIST(0,0, 33,25, 66,50, 80,75, 100,100));				// (OPTIONAL OVERRIDE) (Linear Recommended in ED) *LIST(Axis physical position, Axis output Value)
	}
	
	//Set Throttle Curve two different presets are selectable On The Fly (S3 + RED Button on Throttle Arm)
	int initSetThrottleCurves(){
		vThrottleCurveProfile=!vThrottleCurveProfile;
		if(vThrottleCurveProfile){
			if(!AfterburnerDetent)SetJCurve(&Throttle, THR_RIGHT, 50, 50);	//Without Warthog Physical Afterburner Detent
			else SetJCurve(&Throttle, THR_RIGHT, 65, 95); //At 65% of the physical controller’s axis travel, the DirectX axis must have reached 95% of the axis value.
		}else{
			if(ThrottleAxisRange)SetSCurve(&Throttle, THR_RIGHT, 0, 0, 0, 2, -3);	//Full Range Precision
			else SetSCurve(&Throttle, THR_RIGHT, 0, 0, 0, 3, -3);	//Forward only Precision
		}
		DXAxis(DX_Z_AXIS, -AxisVal(Throttle[THR_RIGHT], &axdata));
		if(DebugMode)printf("Set	Throttle	Curve Profile[%d}\x0a", vThrottleCurveProfile);
	}

	//	-------------------------Set Rudders	
	//Set Rudders Axis
	int initRudders(){
		MapAxis(&TFRPRudder, TRUDDER, DX_Z_AXIS, AXIS_NORMAL, MAP_ABSOLUTE);
		MapAxis(&TFRPRudder, TRPLEFT, DX_X_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
		MapAxis(&TFRPRudder, TRPRIGHT, DX_Y_AXIS, AXIS_REVERSED, MAP_ABSOLUTE);
		if(DebugMode)printf("Initialising Rudders Axis\x0a");
	}
	
	//	-------------------------Set MFD
	//Set Right MFD
	int initRightMFD(){
//		MapKey(&RMFD, GAINU, 0);
//		MapKey(&RMFD, GAIND, 0);
//		MapKey(&RMFD, OSB01, 0);
//		MapKey(&RMFD, OSB02, 0);
//		MapKey(&RMFD, OSB03, 0);
//		MapKey(&RMFD, OSB04, 0);
//		MapKey(&RMFD, OSB05, 0);
//		MapKey(&RMFD, OSB06, 0);
//		MapKey(&RMFD, OSB07, 0);
//		MapKey(&RMFD, OSB08, 0);
//		MapKey(&RMFD, OSB09, 0);
//		MapKey(&RMFD, OSB10, 0);
//		MapKey(&RMFD, OSB11, 0);
//		MapKey(&RMFD, OSB12, 0);
//		MapKey(&RMFD, OSB13, 0);
//		MapKey(&RMFD, OSB14, 0);
//		MapKey(&RMFD, OSB15, 0);
//		MapKey(&RMFD, OSB16, 0);
//		MapKey(&RMFD, OSB17, 0);
//		MapKey(&RMFD, OSB18, 0);
//		MapKey(&RMFD, OSB19, 0);
//		MapKey(&RMFD, OSB20, 0);
//		MapKey(&RMFD, BRTU, 0);
//		MapKey(&RMFD, BRTD, 0);
//		MapKey(&RMFD, CONU, 0);
//		MapKey(&RMFD, COND, 0);
		if(DebugMode)printf("Initialising Right MFD\x0a");
	}
	
	//Set Left MFD
	int initLeftMFD(){
//		MapKey(&LMFD, GAINU, 0);
//		MapKey(&LMFD, GAIND, 0);
//		MapKey(&LMFD, OSB01, 0);
//		MapKey(&LMFD, OSB02, 0);
//		MapKey(&LMFD, OSB03, 0);
//		MapKey(&LMFD, OSB04, 0);
//		MapKey(&LMFD, OSB05, 0);
//		MapKey(&LMFD, OSB06, 0);
//		MapKey(&LMFD, OSB07, 0);
//		MapKey(&LMFD, OSB08, 0);
//		MapKey(&LMFD, OSB09, 0);
//		MapKey(&LMFD, OSB10, 0);
//		MapKey(&LMFD, OSB11, 0);
//		MapKey(&LMFD, OSB12, 0);
//		MapKey(&LMFD, OSB13, 0);
//		MapKey(&LMFD, OSB14, 0);
//		MapKey(&LMFD, OSB15, 0);
//		MapKey(&LMFD, OSB16, 0);
//		MapKey(&LMFD, OSB17, 0);
//		MapKey(&LMFD, OSB18, 0);
//		MapKey(&LMFD, OSB19, 0);
//		MapKey(&LMFD, OSB20, 0);
//		MapKey(&LMFD, BRTU, 0);
//		MapKey(&LMFD, BRTD, 0);
//		MapKey(&LMFD, CONU, 0);
//		MapKey(&LMFD, COND, 0);
		if(DebugMode)printf("Initialising Left MFD\x0a");
	}

//--------------------------------------------------------------------------------------------------------------------------------------------------------------//
//	EVENT HANDLER																																				//
//--------------------------------------------------------------------------------------------------------------------------------------------------------------//

int EventHandle(int type, alias o, int x)
{
	int TRPCOMBO;
	
	// Throttle: LED On-The-FLy Backlight Control
	if(&o == &Throttle & x == THR_FC & Joystick[S3]){
		vThrottleLightBackup = (AxisVal(Throttle[THR_FC], &axdata) - 32766) / 256 * -1;	//HOLD S3 on Joystick & Move Friction Control Axis
		ActKey(PULSE+KEYON+LED (&Throttle, LED_INTENSITY, vThrottleLightBackup));
	}
	
	// Combine rudder toe brakes into new axis, mapped to DX_THROTTLE_AXIS
	if (&o == &TFRPRudder & (x == TRPLEFT | x == TRPRIGHT)) {
		if (TFRPRudder[TRPLEFT] < TFRPRudder[TRPRIGHT]) TRPCOMBO = TFRPRudder[TRPRIGHT];
		else TRPCOMBO = TFRPRudder[TRPLEFT];
		DXAxis (DX_THROTTLE_AXIS, -TRPCOMBO);
	}
	
	//Map the MODE_FILTERED TFRP through Target; we can Trim, set deadzones, J/Scurves, etc.
	if (&o == &TFRPRudder) {
		GetAxisData(&o, x);
		axdata.val = AxisVal(o[x], &axdata);
		if(x == TRPLEFT | x == TRPRIGHT) axdata.val = -axdata.val;	//Reverse the Toe Brakes, TM why u do dis??
		//axdata.locked = 1;										//Uncomment to disable pedals but keep TRPCOMBO axis
		if(!axdata.locked & !axdata.relative) GameOutput(&o, x, axdata.val);
		
	}
	else DefaultMapping(&o, x);
}
